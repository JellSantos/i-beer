plugins {
    id "org.springframework.boot" version "2.3.0.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "net.ltgt.apt" version "0.20"
    id "application"
    id "jacoco"
    id "java"
}

ext {
    mapstructVersion = "1.3.0.Final"
    swaggerVersion = "2.9.2"
}

group = "com.jell.leraning"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = "14"
targetCompatibility = "14"

applicationName = "i-beer"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-web"
//    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "org.apache.commons:commons-collections4:4.3"
    implementation "org.postgresql:postgresql:42.2.5"
    implementation "org.liquibase:liquibase-core"
    implementation "org.yaml:snakeyaml:1.24"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    testImplementation "org.assertj:assertj-core:3.12.2"
    testImplementation "org.mockito:mockito-core:2.26.0"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.springframework.security:spring-security-test"
    testRuntime "com.h2database:h2:1.4.199"
}

bootJar {
    enabled = true
    layered()
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    environment = [
            "BP_JVM_VERSION": "14.*"
    ]
    imageName = "${project.group}/${applicationName}:${project.version}"
}

application {
    mainClassName = "com.jell.learning.IBeerApplication"
}
